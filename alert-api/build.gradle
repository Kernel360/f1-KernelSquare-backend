plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'application'
}

group = 'com.kernelsquare'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    //Modules
    implementation project(":core")
    implementation project(":domain-mysql")
    implementation project(":domain-mongodb")

    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"

    //Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'

    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

    //kafka
    implementation 'org.springframework.kafka:spring-kafka'

    //mongoDB
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    //AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

tasks.named('test') {
    useJUnitPlatform()
}

asciidoctor {
    dependsOn test
}

task copyHTML(type: Copy) {
    dependsOn tasks.findByName("asciidoctor")
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

tasks.bootJar {
    dependsOn tasks.asciidoctor
    dependsOn tasks.getByName("copyHTML")
}

tasks.build {
    dependsOn(tasks.asciidoctor, tasks.copyHTML)
}

tasks.named('jar') {
    enabled = false
}